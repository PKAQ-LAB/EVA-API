plugins {
   id "name.remal.check-dependency-updates" version "1.0.177"
   id "com.jfrog.bintray" version "1.8.4"
}

ext {
    springBootVersion = '2.2.5.RELEASE'
    mysql = "8.0.19"
    fastjson = "1.2.66"
    mybatis_plus_starter = "3.3.1"
    lombok = "1.18.12"
    swagger = "2.9.2"
	knife4j = "2.0.2"
    hutool = "5.2.1"
    jaxb_api = "2.3.1"
    jjwt = "0.9.1"
    auth0 = "3.10.0"
    jasypt = "3.0.2"
    p6spy = "3.8.7"
    flyway = "6.3.0"
    boot_admin = "2.2.2"
    truelicense = "1.33"
    caffeine = "2.8.1"
    weixin = "3.7.0"
    securityTest = "5.3.0.RELEASE"
    alipay = "4.9.28.ALL"
    zxing = "3.4.0"
    velocity = "2.2"

    lib = ["p6spy" : "p6spy:p6spy:${p6spy}",
           "flyway" : "org.flywaydb:flyway-core:${flyway}",
           "mysql" : "mysql:mysql-connector-java:${mysql}",
           "jaxb_api" : "javax.xml.bind:jaxb-api:${jaxb_api}",
           "truelicense" : "de.schlichtherle.truelicense:truelicense-core:${truelicense}",
           "jasypt" : "com.github.ulisesbocchio:jasypt-spring-boot-starter:${jasypt}",
            // lombok
           "lombok" : "org.projectlombok:lombok:${lombok}",
            // cache
           "caffeine" : "com.github.ben-manes.caffeine:caffeine:${caffeine}",
           "redis" : "org.springframework.boot:spring-boot-starter-data-redis:${springBootVersion}",
           "cache" : "org.springframework.boot:spring-boot-starter-cache:${springBootVersion}",
           // AOP
           "aop" : "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}",
           // security
           "security" : "org.springframework.boot:spring-boot-starter-security:${springBootVersion}",
           // web
           "web" : "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",
           "undertow" : "org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}",
            // test
           "test" : "org.springframework.boot:spring-boot-starter-test:${springBootVersion}",
           "securityTest" : "org.springframework.security:spring-security-test:${securityTest}",
            // jpa
           "jpa" : "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}",
           // json
           "fastjson" : "com.alibaba:fastjson:${fastjson}",
           // hutool
           "hutool" : "cn.hutool:hutool-all:${hutool}",
           // mybatis
           "mybatisPlus" : "com.baomidou:mybatis-plus-boot-starter:${mybatis_plus_starter}",
           // swagger
           "swagger" : "io.springfox:springfox-swagger2:${swagger}",
           "knife4j" : "com.github.xiaoymin:knife4j-spring-boot-starter:${knife4j}",
            // jwt
           "auth0" : "com.auth0:java-jwt:${auth0}",
           "jjwt" : "io.jsonwebtoken:jjwt:${jjwt}",
            // wxjava
           "wxmp" : "com.github.binarywang:weixin-java-mp:${weixin}",
           "wxpay" : "com.github.binarywang:weixin-java-pay:${weixin}",
            // ali
           "alipay" : "com.alipay.sdk:alipay-sdk-java:${alipay}",
           "zxing" : "com.google.zxing:core:${zxing}",
            // bootAdmin
           "bootAdminServer" : "de.codecentric:spring-boot-admin-starter-server:${boot_admin}",
           "bootAdmin" : "de.codecentric:spring-boot-admin-starter-client:${boot_admin}",
           "devTools" : "org.springframework.boot:spring-boot-devtools",
            // generator
           "velocity" : "org.apache.velocity:velocity-engine-core:${velocity}",
           "mybatis_plus_generator" : "com.baomidou:mybatis-plus-generator:${mybatis_plus_starter}"
    ]
}

allprojects {
    apply plugin: "name.remal.check-dependency-updates"
    group = 'io.nerv'
    version = '2.2.8'
}

subprojects {
    apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
    
	sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
	
	tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
		options.addStringOption('Xdoclint:none', '-quiet')
    }

    repositories {
        mavenLocal()
		maven { url"https://maven.springframework.org"}
        maven { url"https://repo.spring.io/libs-release" }
		maven { url"https://mirrors.huaweicloud.com" }
        jcenter()
        mavenCentral()
    }

    dependencies {
       implementation "${lib.lombok}"
	   annotationProcessor "${lib.lombok}"

       testImplementation "${lib.test}",
                          "${lib.securityTest}"
     }
}

