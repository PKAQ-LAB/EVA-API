plugins {
   id "name.remal.check-dependency-updates" version "1.0.133"
   id "com.jfrog.bintray" version "1.8.4"
}

ext {
    springBootVersion = '2.1.5.RELEASE'
    mysql = "8.0.16"
    fastjson = "1.2.58"
    mybatis_plus_starter = "3.1.1"
    lombok = "1.18.8"
    swagger = "2.9.2"
	swagger_ui = "1.9.3"
    hutool = "4.5.10"
    p3c = "1.3.6"
    jaxb_api = "2.3.1"
    jjwt = "0.9.1"
    auth0 = "3.8.0"
    sharding_jdbc = "3.0.0"
    jasypt = "2.1.1"
    p6spy = "3.8.2"
    flyway = "5.2.4"
    commons_io = "2.6"
}

allprojects {
    apply plugin: "name.remal.check-dependency-updates"
    group = 'io.nerv'
    version = '1.1.18'
}

subprojects {
	apply plugin: 'pmd'
    apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
    
	sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
	
	tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
		options.addStringOption('Xdoclint:none', '-quiet')
    }
	
    pmd {
        consoleOutput = true
        reportsDir = file("build/reports/pmd")

        ruleSets = [
            "java-ali-comment"
        ]
    }

    repositories {
        mavenLocal()
        maven { url"https://repo.spring.io/libs-release" }
        jcenter()
        mavenCentral()
    }

    dependencies {
       pmd "com.alibaba.p3c:p3c-pmd:${p3c}"
       implementation "org.projectlombok:lombok:${lombok}"
	   annotationProcessor "org.projectlombok:lombok:${lombok}"
     }
}

