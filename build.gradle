plugins {
   id "io.spring.dependency-management" version "${spring_dep}"
   id 'org.springframework.boot' version "${spring_boot_ver}"
}

ext {
    lib = [ "eva_core_common": "io.nerv:eva-core-common:${eva}",
            "eva_core_data_mybatis": "io.nerv:eva-core-data-mybatis:${eva}",
            "eva_web_base": "io.nerv:eva-web-base:${eva}",
            "eva_web_sys": "io.nerv:eva-web-sys:${eva}",
            "hutool" : "cn.hutool:hutool-all:${hutool}",
            "boot" : "org.springframework.boot:spring-boot-dependencies:${spring_boot_ver}",
            "knife4j_cloud":"com.github.xiaoymin:knife4j-micro-spring-boot-starter:${knife4j}",
            "knife4j":"com.github.xiaoymin:knife4j-spring-boot-starter:${knife4j}",
            "jaxb_api" : "javax.xml.bind:jaxb-api:${jaxb_api}",
            // lombok
            "lombok" : "org.projectlombok:lombok:${lombok}",
            // test
            "test" : "org.springframework.boot:spring-boot-starter-test",
            "boot_web": "org.springframework.boot:spring-boot-starter-web",
            // web
            "web" : "org.springframework.boot:spring-boot-starter-web",
            "undertow" : "org.springframework.boot:spring-boot-starter-undertow",
            // spring cloud
            "cloud": "org.springframework.cloud:spring-cloud-dependencies:${cloud}",
            "cloud_bootstrap": "org.springframework.cloud:spring-cloud-starter-bootstrap:${bootstrap}",
            "alibaba":"com.alibaba.cloud:spring-cloud-alibaba-dependencies:${alibaba}",
            // nacos
            "nacos_config":"com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config",
            "nacos_client":"com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery",
            // gateway
            "gateway": "org.springframework.cloud:spring-cloud-starter-gateway",
            "security_config": "org.springframework.security:spring-security-config",
            "loadbalancer": "org.springframework.cloud:spring-cloud-starter-loadbalancer",
            // jwt
            "jwt": "com.nimbusds:nimbus-jose-jwt:${jwt}",
            // OAUTH2 SERVER
            "security": "org.springframework.boot:spring-boot-starter-security",
            "oauth2_config": "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure",
            "oauth2_client": "org.springframework.security:spring-security-oauth2-client",
            "jose": "org.springframework.security:spring-security-oauth2-jose",
            "rsa": "org.springframework.security:spring-security-rsa:${rsa}",
            "resource_server": "org.springframework.security:spring-security-oauth2-resource-server",
    ]
}

allprojects {
    group = 'io.nerv'
    version = '2.5.6'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencyManagement {
        imports {
            mavenBom "${lib.boot}"
            mavenBom "${lib.cloud}"
            mavenBom "${lib.alibaba}"
        }
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
	
	tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
		options.addStringOption('Xdoclint:none', '-quiet')
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url"https://maven.aliyun.com/repository/central" }
        maven { url"https://maven.aliyun.com/repository/jcenter" }
        maven { url"https://mirrors.huaweicloud.com" }
        maven { url"https://maven.aliyun.com/repository/spring" }
        maven { url"https://maven.aliyun.com/repository/spring-plugin" }
		maven { url"https://maven.springframework.org"}
        maven { url"https://repo.spring.io/libs-release" }

        google()
    }

    dependencies {
        implementation "${lib.lombok}",
                       "${lib.nacos_config}",
                       "${lib.nacos_client}",
                       // spring cloud 2020以后的版本,默认不优先读取bootstrap.yml了;
                       // bootstrap.yml失效,导致nacos报错Param 'serviceName' is illegal, serviceName is blank
                       // 相关链接:https://docs.spring.io/spring-cloud-config/docs/current/reference/html/#config-first-bootstrap
                       "${lib.cloud_bootstrap}"

        annotationProcessor "${lib.lombok}"

        testImplementation "${lib.test}"

     }
}

